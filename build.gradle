plugins {
    id "io.spring.dependency-management" version "1.0.6.RELEASE"
}

ext {
    version = '2.0.5-beta'
    mockitoVersion = '4.0.0'
    junitApiVersion = '5.8.1'
    junitPlatformVersion = '1.2.0'
    mockitoJunitVersion = '2.18.0'
    typesafeConfigVersion = '1.4.0'
    slf4jLogVersion = '1.7.30'
    kotlinVersion = '1.5.0'
//    kotlinReflectionVersion = '1.4.21'
}

allprojects {
    apply plugin: 'jacoco'

    group 'org.skelligframework'
    version "${version}"

    repositories {
        mavenLocal()
        mavenCentral()
    }
}

subprojects {

    apply plugin: 'java'
    apply plugin: 'maven-publish'
    apply plugin: 'io.spring.dependency-management'

    version = '2.0.5-beta'

    test {
        reports.html.enabled = false
    }

    ext {
        projectUrl = 'http://skelligframework.org'

        projectRepo = {
            url 'https://github.com/skellig-framework/skellig-core'
            connection 'https://github.com/skellig-framework/skellig-core.git'
        }

        projectLicenses = {
            license {
                name = 'The Apache Software License, Version 2.0'
                url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                distribution = 'repo'
            }
        }

        projectDevelopers = {
            developer {
                id = 'skellig-framework-team'
                name = 'Skellig Framework Development Team'
                roles {
                    role = 'developer'
                    role = 'tester'
                }
            }
        }
    }

    configurations {
        bundler {
            transitive = true
        }

        bundlerTansitive {
            extendsFrom bundler
            transitive = true
        }
    }

    tasks.withType(JavaCompile) {
        options.encoding = 'UTF-8'
    }

    sourceCompatibility = 1.8
    targetCompatibility = 1.8


    jar {
        manifest {
            attributes(
                    "Manifest-Version": "1.0",
                    "Implementation-Title": "${project.name}",
                    "Implementation-Version": "${project.version}",
                    'Implementation-Date': new java.text.SimpleDateFormat("yyyy-MM-dd").format(new Date()),
                    'Created-By': "Gradle ${gradle.gradleVersion}",
                    'Build-Jdk': "${System.properties['java.version']} (${System.properties['java.vendor']} ${System.properties['java.vm.version']})",
            )
        }
    }

    dependencyManagement {
        dependencies {
            dependency group: 'org.slf4j', name: 'slf4j-api', version: "${slf4jLogVersion}"
            dependency group: 'org.slf4j', name: 'slf4j-log4j12', version: "${slf4jLogVersion}"
            dependency group: 'log4j', name: 'log4j', version: '1.2.17'
            dependency group: 'com.typesafe', name: 'config', version: "${typesafeConfigVersion}"
            dependency group: 'org.apache.commons', name: 'commons-lang3', version: '3.12.0'
            dependency group: 'org.freemarker', name: 'freemarker', version: "2.3.30"

            dependency group: 'org.mockito', name: 'mockito-core', version: "${mockitoVersion}"
            dependency group: 'com.nhaarman', name: 'mockito-kotlin', version: '1.6.0'
            dependency group: 'com.nhaarman.mockitokotlin2', name: 'mockito-kotlin', version: '2.2.0'
            dependency group: 'org.junit.jupiter', name: 'junit-jupiter-api', version: "${junitApiVersion}"
            dependency group: 'org.junit.jupiter', name: 'junit-jupiter-engine', version: "${junitApiVersion}"
            dependency group: 'org.junit.platform', name: 'junit-platform-runner', version: "${junitPlatformVersion}"
            dependency group: 'org.junit.platform', name: 'junit-platform-launcher', version: "${junitPlatformVersion}"
            dependency group: 'org.mockito', name: 'mockito-junit-jupiter', version: "${mockitoJunitVersion}"
            dependency group: 'org.jetbrains', name: 'kotlin:kotlin-reflect', version: "${kotlinVersion}"
        }
    }

    java {
        withSourcesJar()
        withJavadocJar()
    }

    test {
        useJUnitPlatform {
            excludeTags "component-test"
            testLogging {
                events "failed"

                showExceptions true
                exceptionFormat "full"
                showCauses true
                showStackTraces true
                showStandardStreams = false
            }
        }
    }

    tasks.register("componentTest", Test) {
        useJUnitPlatform {
            includeTags "component-test"
        }
    }
}

task testReport(type: TestReport) {
    destinationDir = file("$buildDir/reports/tests")
    reportOn subprojects*.test
}

task codeCoverageReport(type: JacocoReport) {
    executionData fileTree(project.rootDir.absolutePath).include("**/build/jacoco/*.exec")

    subprojects.each {
        sourceSets it.sourceSets.main
    }

    reports {
        xml.enabled true
        xml.destination file("${buildDir}/reports/jacoco/report.xml")
        html.enabled false
        csv.enabled false
    }
}

codeCoverageReport.dependsOn {
    subprojects*.test
}