import java.text.SimpleDateFormat

buildscript {
    dependencies {
        classpath 'io.github.gradle-nexus:publish-plugin:1.1.0'
    }
}

plugins {
    id 'maven-publish'
    id "io.spring.dependency-management" version "1.0.6.RELEASE"
    id "io.github.gradle-nexus.publish-plugin" version "1.1.0"
}

apply plugin: 'io.github.gradle-nexus.publish-plugin'

nexusPublishing {
    repositories {
        sonatype {
            stagingProfileId = '5d4516aee10f28'
            username = ossrhUsername
            password = ossrhPassword
            nexusUrl.set(uri("https://s01.oss.sonatype.org/service/local/"))
            snapshotRepositoryUrl.set(uri("https://s01.oss.sonatype.org/content/repositories/snapshots/"))
        }
    }
}

ext {
    group_id = 'org.skelligframework'
    projectVersion = '2.1.1'
    mockitoVersion = '5.10.0'
    junitApiVersion = '5.10.1'
    junitPlatformVersion = '1.10.1'
    typesafeConfigVersion = '1.4.3'
    slf4jLogVersion = '2.0.12'
    log4jLogVersion = '2.22.1'
    kotlinVersion = '1.9.22'
    kotlinCoroutinesVersion = '1.8.0'
    ktMockitoVersion = '5.2.1'
    freemarkerVersion = '2.3.32'
    testContainerVersion = '1.19.4'
    okHttpVersion = '4.12.0'
    jacksonVersion = '2.16.1'
    apacheCommonsVersion = '3.14.0'
}

allprojects {
    apply plugin: 'jacoco'
    apply plugin: 'maven-publish'
    group 'org.skelligframework'
    version "${projectVersion}"

    repositories {
        mavenLocal()
        mavenCentral()
    }
}

subprojects {

    apply plugin: 'java'
    apply plugin: 'maven-publish'
    apply plugin: 'signing'
    apply plugin: 'io.spring.dependency-management'

    version = "${projectVersion}"

    ext {
        project_name = 'Skellig Framework'
        project_description = 'Skellig Framework'
    }

    task sourceJar(type: Jar) {
        classifier "sources"
        from sourceSets.main.allJava
    }

    task javadocJar(type: Jar, dependsOn: javadoc) {
        classifier "javadoc"
        from javadoc.destinationDir
    }

    artifacts {
        archives jar
        archives sourceJar
        archives javadocJar
    }

    jar {
        from sourceSets.main.output
        from sourceSets.main.allJava
        manifest {
            attributes(
                    "Manifest-Version": "1.0",
                    "Implementation-Title": "${project.name}",
                    "Implementation-Version": "${project.version}",
                    'Implementation-Date': new SimpleDateFormat("yyyy-MM-dd").format(new Date()),
                    'Created-By': "Gradle ${gradle.gradleVersion}",
                    'Build-Jdk': "${System.properties['java.version']} (${System.properties['java.vendor']} ${System.properties['java.vm.version']})",
            )
        }
    }

    afterEvaluate {
        publishing {
            publications {
                mavenJava(MavenPublication) {
                    groupId = 'org.skelligframework'
                    afterEvaluate {
                        artifactId = jar.baseName
                    }
                    from components.java
                }

                release(MavenPublication) {
                    groupId = 'org.skelligframework'
                    version = "${projectVersion}"
                    afterEvaluate {
                        artifactId = jar.baseName
                    }

                    from components.java

                    pom {
                        url = 'https://skelligframework.org'
                        name = project_name
                        description = project_description

                        developers {
                            developer {
                                id = 'Shendor'
                                email = 'shendor1988@gmail.com'
                            }
                        }

                        license {
                            name = 'The Apache Software License, Version 2.0'
                            url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                            distribution = 'repo'
                        }

                        scm {
                            connection = 'https://github.com/skellig-framework/skellig-core.git'
                            url = 'https://github.com/skellig-framework/skellig-core'
                        }

                        signing {
                            required { gradle.taskGraph.hasTask("publish") }
                            sign publishing.publications.release
                            sign configurations.archives
                        }
                    }
                }
            }
        }
    }

    test {
        reports.html.enabled = false
    }

    tasks.withType(JavaCompile) {
        options.encoding = 'UTF-8'
    }

    dependencyManagement {
        dependencies {
            dependency group: 'org.slf4j', name: 'slf4j-api', version: "${slf4jLogVersion}"
            dependency group: 'org.slf4j', name: 'slf4j-reload4j', version: "${slf4jLogVersion}"
            dependency group: 'org.apache.logging.log4j', name: 'log4j-core', version: "${log4jLogVersion}"
            dependency group: 'com.typesafe', name: 'config', version: "${typesafeConfigVersion}"
            dependency group: 'org.apache.commons', name: 'commons-lang3', version: "${apacheCommonsVersion}"
            dependency group: 'org.freemarker', name: 'freemarker', version: "${freemarkerVersion}"
            dependency group: 'org.mockito.kotlin', name: 'mockito-kotlin', version: "${ktMockitoVersion}"
            dependency group: 'org.junit.jupiter', name: 'junit-jupiter', version: "${junitApiVersion}"
            dependency group: 'org.junit.jupiter', name: 'junit-jupiter-api', version: "${junitApiVersion}"
            dependency group: 'org.junit.jupiter', name: 'junit-jupiter-engine', version: "${junitApiVersion}"
            dependency group: 'org.junit.platform', name: 'junit-platform-runner', version: "${junitPlatformVersion}"
            dependency group: 'org.junit.platform', name: 'junit-platform-launcher', version: "${junitPlatformVersion}"
            dependency group: 'org.mockito', name: 'mockito-junit-jupiter', version: "${mockitoVersion}"
            dependency group: 'org.jetbrains', name: 'kotlin:kotlin-reflect', version: "${kotlinVersion}"
            dependency group: 'org.jetbrains.kotlinx', name: 'kotlinx-coroutines-core', version: "${kotlinCoroutinesVersion}"
            dependency group: 'com.fasterxml.jackson.core', name: 'jackson-databind', version: "${jacksonVersion}"
            dependency group: 'com.fasterxml.jackson.datatype', name: 'jackson-datatype-jsr310', version: "${jacksonVersion}"
            dependency group: 'org.testcontainers', name: 'testcontainers', version: "${testContainerVersion}"
            dependency group: 'org.testcontainers', name: 'junit-jupiter', version: "${testContainerVersion}"
            dependency group: 'org.testcontainers', name: 'postgresql', version: "${testContainerVersion}"
            dependency group: 'com.squareup.okhttp3', name: 'okhttp', version: "${okHttpVersion}"
        }
    }

    java {
        toolchain {
            languageVersion.set(JavaLanguageVersion.of(17))
        }
        withSourcesJar()
        withJavadocJar()
    }

    test {
        useJUnitPlatform {
            excludeTags "component-test"
            testLogging {
                events "failed"

                showExceptions true
                exceptionFormat "full"
                showCauses true
                showStackTraces true
                showStandardStreams = false
            }
        }
    }

    tasks.register("componentTest", Test) {
        useJUnitPlatform {
            includeTags "component-test"
        }
    }
}

task testReport(type: TestReport) {
    destinationDir = file("$buildDir/reports/tests")
    reportOn subprojects*.test
}

task codeCoverageReport(type: JacocoReport) {
    executionData fileTree(project.rootDir.absolutePath).include("**/build/jacoco/*.exec")

    subprojects.each {
        sourceSets it.sourceSets.main
    }

    reports {
        xml.enabled true
        xml.destination file("${buildDir}/reports/jacoco/report.xml")
        html.enabled false
        csv.enabled false
    }
}

codeCoverageReport.dependsOn {
    subprojects*.test
}